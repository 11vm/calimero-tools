buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

apply plugin:'java'
apply plugin:'application'

version = '2.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'tuwien.auto.calimero.tools.Main'

run {
    // for passing program arguments, use -Dexec.args="..."
    args System.getProperty("exec.args", "").split() 
    standardInput = System.in
}

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

dependencies {
    compile "com.github.calimero:calimero-core:${version}"
    runtime "com.github.calimero:calimero-rxtx:${version}"
    compile 'org.slf4j:slf4j-api:1.7.22'
    runtime 'org.slf4j:slf4j-simple:1.7.22'
}

jar {
	manifest {
		attributes(
			'Main-Class': mainClassName,
			'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
		)
	}
}
